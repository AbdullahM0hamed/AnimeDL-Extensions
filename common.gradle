apply plugin: 'org.jmailen.kotlinter'

android {
    compileSdkVersion 29

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    sourceSets {
        main {
            manifest.srcFile "$rootDir/stubs/src/main/AndroidManifest.xml"
            java.srcDirs = ['src']
            res.srcDirs = ['res']
        }
        release {
            manifest.srcFile "AndroidManifest.xml"
        }
        debug {
            manifest.srcFile "AndroidManifest.xml"
        }
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        applicationIdSuffix extName
        versionCode extVersionCode
        setProperty("archivesBaseName", "animedl-$extName-v$versionCode")
        manifestPlaceholders = [
                appName : "AnimeDL: $extName",
                extClass: extClass,
        ]
    }

    dependenciesInfo {
        includeInApk = false
    }

    buildFeatures {
        // Disable unused AGP features
        aidl false
        renderScript false
        resValues false
        shaders false
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

repositories {
    mavenCentral()
}

apply from: "$rootDir/common-dependencies.gradle"

preBuild.dependsOn(lintKotlin)
lintKotlin.dependsOn(formatKotlin)

tasks.register("compileDex") {
    actions.clear()
    doLast {
        CompileDex.compile(project.extensions, project.buildDir, project.objects)
    }
}
